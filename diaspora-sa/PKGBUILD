# Maintainer: David H. Bronke <whitelynx@gmail.com>
# Contributor: Ezekiel Sulastin <zekesulastin@gmail.com> 
# Contributor: Mr_Robotic_Evil <mr.robotic.evil@googlemail.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

# Warning: This package is BIG; you'll probably need at least 7 GiB free in
#   order to build. (1.3 GiB for the downloads alone, about 3 GiB for the
#   unpacked game data and source, and 2 GiB for the finished package if
#   uncompressed)  'mv' is used over 'cp' in the package phase to save a bit
#   of space, but make sure there is enough room for everything.

# This PKGBUILD is based on fs2_open and fs2_open-data from AUR; the
# maintainers and contributors of those packages have been listed as
# contributors above.

pkgname=diaspora-sa
pkgver=1.0.2
pkgrel=1
pkgdesc="Diaspora: Shattered Armistice is a single and multiplayer space fighter combat game set in the reimagined Battlestar Galactica universe."
url="http://diaspora.hard-light.net/?nowreleased"
arch=('i686' 'x86_64')
license=('CCPL' 'custom:fs2_open')
depends=('libjpeg' 'libpng' 'libtheora' 'libvorbis' 'lua' 'mesa' 'openal' 'sdl')
optdepends=('wxlauncher: a launcher for fs2_open-based games')
install=diaspora-sa.install
source=(http://diaspora.fs2downloads.com/Diaspora_R1_Linux.tar.lzma
        http://diaspora.fs2downloads.com/Diaspora_R1_Patch_$pkgver.tar.lzma
        'osapi_unix.patch'
        'increase_joy_buttons_fixed.patch'
        'diaspora-sa'
        'diaspora-sa.conf')

# You can try to save some build time by using MD5 or SHA1 instead of SHA256
# here, but in my experience the difference isn't really noticeable next to
# the build time. (and SHA256 is more secure)
#md5sums=('22b55ae9bc9366ccbeb1642cd50dc3f8'
#         'c2075bcf2b9273a9149c834aabc81d59'
#         '783d5ab68a0ce4d26ee415e8fefbc762'
#         '892cee11520d6e258eb17e897f98c1c9'
#         '23fded0ca796c79a66127c26b22afe3c'
#         '63383810fe6b92b9c3230410e127dfa8')
#sha1sums=('b7f73e8fac2663acbed1b9363558554caa706159'
#          '1df904b6fdbd8ebda83badb564b1c0e09c3f86d1'
#          '8e475f6ce90d92e9c10545d6c2cca4efee625b04'
#          'e7c80ec8c25ef1f377341c0bc3bcb5ab212a121b'
#          '294fb24c27dfd9f643bb85fd637fc5929c1a7c6b'
#          'db78e25b4625ff5ef0f728fb6458db221821ce07')
sha256sums=('598910f2382ab2c8b10aa02cc6fb00c55ed4cd5a1a64d580deb1a0d4e521cf17'
            '24314ab1267c6ce910cf14756125b27b9c18d41d454680798f321ce55accea82'
            '4a33d78d70ef97f661125255e3be6f1d8fda38deca996568515d7c1218e53c9e'
            'b583962f6be24366246e4a85c0874a5eb649f685c51b0b37a848b3ba9f72ed71'
            'b381f7b095ca3d2c4678ed8920cde4e8635bf69f29cfe633bae309e117599984'
            'd2e18d9601d3f37fa76111ff19f003de49fc520d1a211881e2bf904158e4fd33')

# This package is about 2 GiB uncompressed and takes a while to recompress for
# not too much space savings; the following PKGEXT disables compression of the
# package.  Add .xz or similar to the end of PKGEXT to compress the package.
PKGEXT=".pkg.tar"


build() {
  local SOURCE="$srcdir/Diaspora_R1_Linux/Diaspora/"

  msg "Removing unneeded files..."
  rm -r $SOURCE/resources $SOURCE/wxlauncher

  msg "Patching..."

  # 1.0.2 patch
  tar -xf $srcdir/Diaspora_R1_Patch_$pkgver/Patch_Files.$pkgver.tar -C $SOURCE

  cd $SOURCE/fs2_open
	# Changes default video settings for better mod compatability
	patch -Np0 -i "$srcdir/osapi_unix.patch"
	
	# Increases hard limit of joystick buttons for better use with HOTAS etc.
	patch -Np0 -i "$srcdir/increase_joy_buttons_fixed.patch"


  # (2) Build the game engine (fs2_open)
  msg "Building..."
  cd $SOURCE/fs2_open
	./autogen.sh --enable-speech
  make


  # (4) Set up the Diaspora launcher profile (for wxlauncher)
  cd $SOURCE
  sed "s@^folder=.*\$@folder=/opt/$pkgname@;s@pro00099.template.ini@@" pro00099.template.ini > pro00099.ini


  # Grab command-line options from pro00099.ini for our runner script.
  cd $srcdir
  FS2_OPTS="$(sed -n 's@^flags=\(.*\)$@\1@p' $SOURCE/pro00099.ini | tr -d '\r\n')"
  sed -i "s@\\(FS2OPTS=\\).*@\\1\"$FS2_OPTS\"@" $srcdir/diaspora-sa.conf
}

package() {
  local SOURCE="$srcdir/Diaspora_R1_Linux/Diaspora/"

  cd $SOURCE

  #install -D -m644 -t "$pkgdir/opt/$pkgname/" *.vp *.ini *.png *.bmp data
  install -m644 -d "$pkgdir/opt/$pkgname/"
  mv *.vp *.png *.bmp mod.ini pro00099.ini data "$pkgdir/opt/$pkgname/"

  #install -D -m644 -t "$pkgdir/usr/share/doc/$pkgname/" *.pdf *.txt *.rtf
  install -m644 -d "$pkgdir/usr/share/doc/$pkgname/"
  mv *.pdf *.txt *.rtf "$pkgdir/usr/share/doc/$pkgname/"

  # Fix permissions (since 'mv' won't set them)
  chmod -R 644 "$pkgdir/opt/$pkgname/" "$pkgdir/usr/share/doc/$pkgname/"
  find "$pkgdir/opt/$pkgname/" "$pkgdir/usr/share/doc/$pkgname/" -type d -exec chmod 755 '{}' \;

	install -D -m755 $srcdir/diaspora-sa "$pkgdir/usr/bin/diaspora-sa"
	install -D -m644 $srcdir/diaspora-sa.conf "$pkgdir/etc/$pkgname/diaspora-sa.conf"

	install -D -m644 fs2_open/COPYING "$pkgdir/usr/share/licenses/$pkgname/fs2_open-license"
	install -D -m755 fs2_open/code/fs2_open_3.6.13 "$pkgdir/opt/$pkgname/fs2_open_diaspora"
}

# vim:set ts=2 sw=2 et:
