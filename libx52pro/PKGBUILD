# Maintainer: ZekeSulastin <zekesulastin+aur@gmail.com>
pkgname=libx52pro
pkgver=0.1.1
pkgrel=1
pkgdesc="Library (with example) to interface with the MFD+LEDs of the Saitek X52 FCS and X52 Pro FCS/Yoke"
arch=(i686 x86_64)
url="http://plasma.hasenleithner.at/x52pro/"
license=('LGPL')
depends=('libusb-compat')
install='libx52pro.install'
source=(${url}x52pro-$pkgver.tar.gz)
md5sums=('ba722a9dad498eb40e601bbcfc5f3002')

_group=games # This group will have write-access to the joystick itself, necessary to change MFD/LED settings.  Change this as necessary for your setup.

build() {
  cd "$srcdir/x52pro-$pkgver"
  sed -i -e s/SYSFS/ATTR/g 99-x52pro.rules # Old syntax
  sed -i -e s/plugdev/$_group/g 99-x52pro.rules # Allow user-configurable group for access
  sed -i -e s/usb_device/usb/g 99-x52pro.rules # usb_device is deprecated
  make
}

package() {
  cd "$srcdir/x52pro-$pkgver"
  make DESTDIR="$pkgdir/" install
}
