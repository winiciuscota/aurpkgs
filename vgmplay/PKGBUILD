# Maintainer: ZekeSulastin <zekesulastin+aur@gmail.com>

pkgname=vgmplay
pkgver=20121111
pkgrel=1
pkgdesc="The offical and always up-to-date player for all VGM files"
arch=('i686' 'x86_64')
url="http://vgm.mdscene.net/forum/viewtopic.php?t=112"
source=('http://vgm.mdscene.net/programs/players/VGMPlay_src.rar'
        'vgmplay.1'
        'vgmplay')
md5sums=('ed184fb0689f3b640b285b25229337b2'
         '7e43dca20f1560c4b12a7fe38a8d945f'
         '04f624ee0ec81118160dbf9c7957d19b')
license=('GPL') # best guess, based on provided credits
depends=('zlib' 'bash')
makedepends=('unrar')
optdepends=("oss: or your sound system's oss wrapper")
noextract=('VGMPlay_src.rar')
install='vgmplay.install'

build() {
  unrar x VGMPlay_src.rar # bsdtar does not support solid RAR archives
  sed -i -e s@zlib/zlib.h@zlib.h@g *.c
  make -j1 # breaks with -j>1
}

package() {
  install -D -m755 VGMPlay "$pkgdir/usr/bin/VGMPlay"
  install -D -m644 VGMPlay.ini "$pkgdir/usr/share/vgmplay/VGMPlay.ini"
  install -D -m644 vgmplay.1 "$pkgdir/usr/share/man/man1/vgmplay.1"

  # VGMPlay requires its ini file to be in the current working directory.
  # This wrapper makes it start in $XDG_CONFIG_HOME and provides --help
  #   and --version information.
  # The launcher script also automatically uses padsp if pulseaudio is
  #   running; edit the launcher if you don't want that behavior. 

  install -D -m755 vgmplay "$pkgdir/usr/bin/vgmplay"
}
