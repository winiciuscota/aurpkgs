# Maintainer: ZekeSulastin <zekesulastin+aur@gmail.com>

pkgname=vgmplay
pkgver=0.40.3
epoch=1
_srcver=040-3
pkgrel=1
pkgdesc="The offical and always up-to-date player for all VGM files"
arch=('i686' 'x86_64')
url="http://vgm.mdscene.net/forum/viewtopic.php?t=112"
source=("http://vgm.mdscene.net/programs/players/VGMPlay_${_srcver}_src.7z"
        'vgmplay.1'
        'vgmplay')
md5sums=('4867e94f9e6405fd10f1e5aee346ef1c'
         '3f9962e1f4b08cae44eb6d09ecd6ddb9'
         '40ae2a521f4c6b5ca6a1566855c8e596')
license=('GPL') # Due to included libs
depends=('zlib' 'bash')
optdepends=("oss: program's native sound system"
            "alsa-oss: ALSA's: aoss wrapper"
            "pulseaudio: PulseAudio's padsp wrapper"
            "ossp: provides OSS emulation via CUSE")
install='vgmplay.install'

build() {
  cd "$srcdir"
  # Quick fix to move binary out of the way of the script while allowing
  #  use of the current config file VGMPlay.ini
  sed -i -e 's@-o vgmplay@-o VGMPlay@g' makefile
  make -j1
}

package() {
  install -D -m755 VGMPlay "$pkgdir/usr/bin/VGMPlay"
  install -D -m644 VGMPlay.ini "$pkgdir/usr/share/vgmplay/VGMPlay.ini"
  install -D -m644 vgmplay.1 "$pkgdir/usr/share/man/man1/vgmplay.1"
  install -D -m755 vgmplay "$pkgdir/usr/bin/vgmplay"
}
